plugins {
    id 'java'

    // ensure archiveBaseName is update
    id 'io.deephaven.common-conventions'

    // ensure project.ext.license is set
    id 'io.deephaven.java-license-conventions'
}

tasks.withType(Jar).configureEach {
    Jar jar ->
        // Ensure that the JARs are binary stable regardless of when they were built
        jar.preserveFileTimestamps = false
        jar.reproducibleFileOrder = true
        jar.manifest {
            attributes(
                    'Specification-Title': archiveBaseName,
                    'Specification-Version': project.version,
                    'Specification-Vendor': 'Deephaven',
                    'Implementation-Title': archiveBaseName,
                    'Implementation-Version': project.version,
                    'Implementation-Vendor': 'Deephaven',
                    'Implementation-Vendor-Id': project.group,
                    'Implementation-URL': 'https://github.com/deephaven/deephaven-core',
                    'Bundle-License': project.ext.license.url)
        }
}
