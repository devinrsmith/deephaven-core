import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

plugins {
    id 'com.bmuschko.docker-remote-api'
}

evaluationDependsOn ':deephaven-jpy'
evaluationDependsOn ':Integrations'
evaluationDependsOn ':grpc-api-server-native'

def dockerContext = project.layout.buildDirectory.dir('context')
def commonDir = rootProject.layout.projectDirectory.dir('docker/common')

def deephavenJpyWheel = project(':deephaven-jpy').tasks.getByName('buildDockerForWheel')
def deephavenWheel = project(':Integrations').tasks.getByName('buildDockerForWheel')
def deephaven2Wheel = project(':Integrations').tasks.getByName('buildDockerForWheel2')
def grpcApiTar = project(':grpc-api-server-native').tasks.getByName('distTar')

def prepareDocker = project.tasks.register('prepareDocker', Sync) {
    it.from (commonDir) {
        into 'common'
    }
    it.from (grpcApiTar.outputs.files) {
        rename { file -> 'server.tar' }
        into 'server'
    }
    it.from 'src/main/docker'
    it.into dockerContext
}

def buildImage = { String name, String imageName, String outImage, TaskProvider<? extends Task> checkImageTask ->
    Docker.registerDockerImage(project, name) {
        dependsOn prepareDocker
        inputs.files deephavenJpyWheel.outputs.files
        inputs.files deephavenWheel.outputs.files
        inputs.files deephaven2Wheel.outputs.files
        inputs.files checkImageTask.get().outputs.files
        inputDir.set dockerContext
        // TODO: we don't have to explicitly set this if we use buildx
        buildArgs.put('TARGETARCH', 'amd64')
        buildArgs.put('BASE', imageName)
        images.add(outImage)
    } as TaskProvider<? extends DockerBuildImage>
}

def buildImageType = { String type ->
    def imageId = Docker.lookupImageId(project, project.property("deephaven.server${type}.base"))
    def checkImageTask = Docker.registerPullAndCheckImage(project, type, imageId)
    return buildImage("buildImage${type}", imageId, "deephaven/server${type}:local-build", checkImageTask)
}

def types = [ '', '-nltk', '-pytorch', '-sklearn', '-tensorflow' ]

def builds = types.collect { buildImageType(it) }

assemble.dependsOn builds

// To print out the latest:
// ./gradlew -q server:showBaseImages -Pdeephaven.docker.pullMode=always
project.tasks.register('showBaseImages') {
    it.dependsOn project.tasks.findAll { task -> task.name.startsWith('showImage') }
}
