plugins {
    id 'com.bmuschko.docker-remote-api'
}

evaluationDependsOn ':deephaven-jpy'
evaluationDependsOn ':Integrations'
evaluationDependsOn ':grpc-api-server-native'

def deephavenJpyWheel = project(':deephaven-jpy').tasks.getByName('buildDockerForWheel')
def deephavenWheel = project(':Integrations').tasks.getByName('buildDockerForWheel')
def deephaven2Wheel = project(':Integrations').tasks.getByName('buildDockerForWheel2')

def grpcApiTar = project(':grpc-api-server-native').tasks.getByName('distTar')

def dockerContext = project.layout.buildDirectory.dir('context')

def prepareDocker = project.tasks.register('prepareDocker', Sync) {
    it.from (grpcApiTar.outputs.files) {
        rename { file -> 'server.tar' }
    }
    it.from 'src/main/docker'
    it.into dockerContext
}

def buildDocker = Docker.registerDockerImage(project, 'buildDocker') {
    dependsOn prepareDocker
    inputs.files deephavenJpyWheel.outputs.files
    inputs.files deephavenWheel.outputs.files
    inputs.files deephaven2Wheel.outputs.files
    inputDir.set dockerContext
    images.add("deephaven/${project.name}:local-build")
}

assemble.dependsOn buildDocker