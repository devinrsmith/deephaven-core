plugins {
    id 'com.bmuschko.docker-remote-api'
}

evaluationDependsOn ':grpc-api-server-native'

def baseImage = Docker.lookupImageId(project, project.property('deephaven.server-slim.base'))
def outImage = project.property('deephaven.server-slim.outImage')

def dockerContext = project.layout.buildDirectory.dir('context')
def commonDir = rootProject.layout.projectDirectory.dir('docker/common')

def grpcApiTar = project(':grpc-api-server-native').tasks.getByName('distTar')

def prepareDocker = project.tasks.register('prepareDocker', Sync) {
    it.from (commonDir) {
        into 'common'
    }
    it.from (grpcApiTar.outputs.files) {
        rename { file -> 'server.tar' }
        into 'server'
    }
    it.from 'src/main/docker'
    it.into dockerContext
}

def checkImage = Docker.registerPullAndCheckImage(project, '', baseImage)

def buildDocker = Docker.registerDockerImage(project, 'buildDocker') {
    dependsOn prepareDocker
    inputDir.set dockerContext
    inputs.files checkImage.get().outputs.files
    // TODO: we don't have to explicitly set this if we use buildx
    buildArgs.put('TARGETARCH', 'amd64')
    buildArgs.put('BASE', baseImage)
    images.add(outImage)
}

assemble.dependsOn buildDocker

// To print out the latest:
// ./gradlew -q server-slim:showBaseImages -Pdeephaven.docker.pullMode=always
project.tasks.register('showBaseImages') {
    it.dependsOn project.tasks.findAll { task -> task.name.startsWith('showImage') }
}
